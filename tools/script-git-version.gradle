buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.ajoberstar:grgit:2.3.0'
    }
}

import org.ajoberstar.grgit.Grgit

/**
 * git.describe()
 *
 * Find the most recent tag that is reachable from HEAD. If the tag points to the commit,
 * then only the tag is shown. Otherwise, it suffixes the tag name with the number of additional
 * commits on top of the tagged object and the abbreviated object name of the most recent commit.
 *
 * More info: https://git-scm.com/docs/git-describe
 */

ext {
    git = Grgit.open(currentDir: projectDir)

    gitBranchName = git.branch.current.name

    gitVersionInfo = git.describe(match: ["v[0-9]*\\.[0-9]*\\.[0-9]*"]).split("-") as List

    gitTagInfo = gitVersionInfo.remove(0).split("v") as List
    gitTagName = gitTagInfo.remove(1)

    gitTagSuffix = (gitVersionInfo.size() >= 1) ? gitVersionInfo.remove(0) : ""
    gitHasSuffix = gitTagSuffix.matches(["(alpha|beta|rc)[0-9]*"])

    gitVersionName = (gitHasSuffix) ?
            [gitTagName, gitTagSuffix].join("-") :
            gitTagName

    gitVersionCode = git.tag.list().size()
    gitVersionCodeTime = git.head().time
}

task printVersion() {
    println("Version Name: $gitVersionName")
    println("Version Code: $gitVersionCode")
    println("Version Code Time: $gitVersionCodeTime")
    println("Branch name: $gitBranchName")
    println("Lib name: $GROUP:$POM_ARTIFACT_ID:$gitVersionName")
}